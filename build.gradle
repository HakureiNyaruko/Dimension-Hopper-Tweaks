//file:noinspection GroovyUnusedAssignment

plugins {
    id 'net.minecraftforge.gradle' version '5.1+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

fancyGradle {
    patches {
        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}


version = '1.9.0'
group = 'mods.thecomputerizer.dimhoppertweaks'
archivesBaseName = 'dimhoppertweaks'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        def args = [
                "-Dmixin.debug.export=true",
                '-Dmixin.hotSwap=true',
                '-Dmixin.checks.interfaces=true'
        ]
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }
    }
}

sourceSets.main.resources { srcDir 'src/main/resources' }

repositories {
    google()
    mavenCentral()
    maven {
        url = 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    compileOnly(fg.deobf('curse.maven:zollern-galaxy-241940:4046635'))

    implementation fg.deobf('curse.maven:codechicken-lib-242818:2779848')
    implementation fg.deobf('curse.maven:fermion-lib-345538:3186519')
    implementation fg.deobf('curse.maven:avaritia-261348:3143349')
    implementation fg.deobf('curse.maven:sgcraft-289115:3044648')
    implementation fg.deobf('curse.maven:reskillable-286382:2815686')
    implementation fg.deobf('curse.maven:had-enough-items-557549:4109510')
    implementation fg.deobf('curse.maven:dimensionaldoors-284876:3785565')
    implementation fg.deobf('curse.maven:industrial-craft-242638:3078604')
    implementation fg.deobf('curse.maven:music-triggers-518410:3796636')
    implementation fg.deobf('curse.maven:extra-utilities-225561:2678374')
    implementation fg.deobf('curse.maven:reskillable-286382:2815686')
    implementation fg.deobf('curse.maven:tinkers-construct-74072:2902483')
    implementation fg.deobf('curse.maven:geckolib-388172:4020277')
    implementation fg.deobf('curse.maven:the-twilight-forest-227639:3051450')

    implementation fg.deobf('curse.maven:mixin-booter-419286:4090558')

    runtimeOnly(fg.deobf('curse.maven:mantle-74924:2713386'))
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': "1.12.2"
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "dimhoppertweaks.refmap.json"
}

jar {
    manifest {
        attributes([
                'FMLAT': 'accesstransformer.cfg',
                'FMLCorePlugin': 'mods.thecomputerizer.dimhoppertweaks.core.EarlyMixinPlugin',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true'
        ])
    }
}