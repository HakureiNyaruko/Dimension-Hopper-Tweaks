//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'net.minecraftforge.gradle' version '5.1+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

fancyGradle {
    patches {
        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}


version = "${project.minecraft_version}-${project.mod_version}"
group = project.maven_group
archivesBaseName = project.base_name

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'stable', version: project.mappings_version
    accessTransformer = file('src/main/resources/META-INF/dimhoppertweaks_at.cfg')
    runs {
        def args = [
                '-Dmixin.debug.export=true',
                '-Dmixin.hotSwap=true',
                '-Dmixin.checks.interfaces=true'
        ]
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }
    }
}

repositories {
    maven {
        url = 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name = 'Sponge'
        url = uri('https://repo.spongepowered.org/maven')
    }
    maven {
        name = 'blamejared'
        url = uri('https://maven.blamejared.com')
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    //galacticraft doesn't seem to work in dev?
    compileOnly(fg.deobf("curse.maven:zollern-galaxy-241940:${project.zollern_version}"))
    compileOnly(fg.deobf("curse.maven:ore-stages-290201:${project.orestages_version}"))
    //compileOnly(fg.deobf('curse.maven:item-stages-2810185:2810185'))

    implementation(fg.deobf("curse.maven:codechicken-lib-242818:${project.ccl_version}"))
    implementation(fg.deobf("curse.maven:avaritia-261348:${project.avaritia_version}"))
    implementation(fg.deobf("curse.maven:dimensionaldoors-284876:${project.dimdoors_version}"))
    implementation(fg.deobf("curse.maven:sgcraft-289115:${project.sgcraft_version}"))
    implementation(fg.deobf("curse.maven:reskillable-286382:${project.reskillable_version}"))
    implementation(fg.deobf("curse.maven:tinkers-construct-74072:${project.tconstruct_version}"))
    implementation(fg.deobf("curse.maven:geckolib-388172:${project.gecko_version}"))
    implementation(fg.deobf("curse.maven:the-twilight-forest-227639:${project.tf_version}"))
    implementation(fg.deobf("curse.maven:psi-241665:${project.psi_version}"))
    implementation(fg.deobf("curse.maven:actually-additions-228404:${project.aa_version}"))
    implementation(fg.deobf("curse.maven:the-one-probe-245211:${project.top_version}"))
    implementation(fg.deobf("curse.maven:xl-food-mod-247217:${project.xlfood_version}"))
    implementation(fg.deobf("curse.maven:game-stages-268655:${project.gamestages_version}"))
    implementation(fg.deobf("curse.maven:aquaculture-60028:${project.aquaculture_version}"))
    implementation(fg.deobf("curse.maven:lockyz-extra-dimensions-mod-283789:${project.lockyz_version}"))
    implementation(fg.deobf("curse.maven:tiny-progressions-250850:${project.tp_version}"))
    implementation(fg.deobf("curse.maven:overloaded-264601:${project.overloaded_version}"))

    //don't need these to compile
    runtimeOnly(fg.deobf("curse.maven:mantle-74924:${project.mantle_version}"))
    runtimeOnly(fg.deobf("curse.maven:autoreglib-250363:${project.autoreglib_version}"))
    runtimeOnly(fg.deobf("curse.maven:had-enough-items-557549:${project.hei_version}"))
    runtimeOnly(fg.deobf("curse.maven:bookshelf-228525:${project.bookshelf_version}"))

    //mixin stuff
    implementation(fg.deobf("curse.maven:mixin-booter-419286:${project.mixinbooter_version}"))
    annotationProcessor "org.spongepowered:mixin:${project.mixin_version}:processor"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft_version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft_version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "dimhoppertweaks.refmap.json"
}

jar {
    manifest {
        attributes([
                'FMLCorePlugin': 'mods.thecomputerizer.dimhoppertweaks.core.EarlyMixinPlugin',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true',
                'FMLAT': 'dimhoppertweaks_at.cfg',
        ])
    }
}