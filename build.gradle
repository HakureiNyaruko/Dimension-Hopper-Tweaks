//file:noinspection GroovyUnusedAssignment
//file:noinspection GroovyAssignabilityCheck

plugins {
    id 'net.minecraftforge.gradle' version '5.1+'
    id 'wtf.gofancy.fancygradle' version '1.1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

fancyGradle {
    patches {
        resources
        coremods
        codeChickenLib
        asm
        mergetool
    }
}


version = '2.0.0'
group = 'mods.thecomputerizer.dimhoppertweaks'
archivesBaseName = 'dimhoppertweaks'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'stable', version: '39-1.12'
    accessTransformer = file('src/main/resources/META-INF/dimhoppertweaks_at.cfg')
    runs {
        def args = [
                "-Dmixin.debug.export=true",
                '-Dmixin.hotSwap=true',
                '-Dmixin.checks.interfaces=true'
        ]
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            jvmArgs args
        }
    }
}

repositories {
    maven {
        url = 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
    maven {
        name = 'Sponge'
        url = uri('https://repo.spongepowered.org/maven')
    }
    maven {
        name = 'blamejared'
        url = uri('https://maven.blamejared.com')
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'

    //galacticraft doesn't seem to work in dev?
    compileOnly(fg.deobf('curse.maven:zollern-galaxy-241940:4046635'))
    compileOnly(fg.deobf('curse.maven:ore-stages-290201:2632997'))
    //compileOnly(fg.deobf('curse.maven:item-stages-2810185:2810185'))

    implementation(fg.deobf('curse.maven:codechicken-lib-242818:2779848'))
    implementation(fg.deobf('curse.maven:avaritia-261348:3143349'))
    implementation(fg.deobf('curse.maven:dimensionaldoors-284876:3926161'))
    implementation(fg.deobf('curse.maven:sgcraft-289115:3044648'))
    implementation(fg.deobf('curse.maven:music-triggers-518410:3796636'))
    implementation(fg.deobf('curse.maven:reskillable-286382:2815686'))
    implementation(fg.deobf('curse.maven:tinkers-construct-74072:2902483'))
    implementation(fg.deobf('curse.maven:geckolib-388172:4020277'))
    implementation(fg.deobf('curse.maven:the-twilight-forest-227639:3051450'))
    implementation(fg.deobf('curse.maven:psi-241665:3085917'))
    implementation(fg.deobf('curse.maven:actually-additions-228404:3117927'))
    implementation(fg.deobf('curse.maven:the-one-probe-245211:2667280'))
    implementation(fg.deobf('curse.maven:xl-food-mod-247217:2740774'))
    implementation(fg.deobf('curse.maven:game-stages-268655:2951840'))
    implementation(fg.deobf('curse.maven:aquaculture-60028:2659258'))

    //don't need these to compile
    runtimeOnly(fg.deobf('curse.maven:mantle-74924:2713386'))
    runtimeOnly(fg.deobf('curse.maven:autoreglib-250363:2746011'))
    runtimeOnly(fg.deobf('curse.maven:had-enough-items-557549:4109510'))
    runtimeOnly(fg.deobf('curse.maven:bookshelf-228525:2836960'))

    //mixin stuff
    implementation(fg.deobf('curse.maven:mixin-booter-419286:4090558'))
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': "1.12.2"
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "dimhoppertweaks.refmap.json"
}

jar {
    manifest {
        attributes([
                "FMLAT": "dimhoppertweaks_at.cfg",
                'FMLCorePlugin': 'mods.thecomputerizer.dimhoppertweaks.core.EarlyMixinPlugin',
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': 'true'
        ])
    }
}